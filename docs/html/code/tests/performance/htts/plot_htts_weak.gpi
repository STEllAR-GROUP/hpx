###############################################################################
## CONFIGURATION
###############################################################################

###############################################################################
# Load header
load HEADER 

###############################################################################
# Global settings 

TITLE="Weak-Scaled HTTS Benchmark (Local Priority Scheduler, NUMA Sensitivity Off)\nAriel (2-socket Intel Sandy Bridge E5-2690, 2.90GHz)\n\n"

set macro
set encoding utf8

set border 3
set tics nomirror out

set key out right center

set grid ytics lc rgb "#bbbbbb" lw 4 lt 0

set xtics 0, 1, 16

set style line 1 lc rgb 'dark-red'         lt 1 lw 2 pt 5 ps 0.2
set style line 2 lc rgb 'dark-orange'      lt 1 lw 2 pt 5 ps 0.2
set style line 3 lc rgb '#9B870C'          lt 1 lw 2 pt 5 ps 0.2
set style line 4 lc rgb 'dark-green'       lt 1 lw 2 pt 5 ps 0.2
set style line 5 lc rgb '#008B8B'          lt 1 lw 2 pt 5 ps 0.2
set style line 6 lc rgb 'dark-blue'        lt 1 lw 2 pt 5 ps 0.2
set style line 7 lc rgb 'dark-violet'      lt 1 lw 2 pt 5 ps 0.2

###############################################################################
# Global variables 

# Get the version from the file
DATE=system(sprintf("cat %s | grep 'VERSION' | awk '{ print $4 }'", INPUT))

# Count the number of blocks in the datafile
stats INPUT every ::1 nooutput
BLOCKS=STATS_blocks

UNCERTAINTY_MULT(f, a, sa, b, sb) = f*sqrt((sa/a)**2+(sb/b)**2)

BLOCKS_STR=sprintf("%i", BLOCKS)
TOTAL_LENGTH="0.5"
BARWIDTH="(".TOTAL_LENGTH."/".BLOCKS_STR.")"
OSTHRDS_BAR="($".OSTHRDS."+".BARWIDTH."*i-".TOTAL_LENGTH."/2+".BARWIDTH."/2)"

BARWIDTH2="(".TOTAL_LENGTH."/2)"
OSTHRDS_BAR2="($".OSTHRDS."+".BARWIDTH2.\
             "*(i-(".BLOCKS_STR."-2))-".TOTAL_LENGTH."/2+".BARWIDTH2."/2)"

###############################################################################
# Terminal configuration
 
set terminal pdf size 11in,8.5in enhanced font "Arial"
#set output "htts_".DATE.".pdf"
set output OUTPUT

###############################################################################
## SPEEDUP, KTEPS
###############################################################################

#set multiplot layout 2,1 scale 0.9

###############################################################################
# SPEEDUP

norms_avg=""
norms_std=""

do for [i=0:BLOCKS-1] {
    stats INPUT index i using @WTIME_AVG:@OSTHRDS name "AVG" nooutput
    if (i==0) { norms_avg=sprintf("%f", AVG_pos_min_y) }
    else { norms_avg=norms_avg.sprintf(" %f", AVG_pos_min_y) }

    stats INPUT index i using @WTIME_STD:@OSTHRDS name "STD" nooutput
    if (i==0) { norms_std=sprintf("%f", STD_pos_min_y) }
    else { norms_std=norms_std.sprintf(" %f", STD_pos_min_y) }
}

speedup_avg="(word(norms_avg,i+1)*$".OSTHRDS."/$".WTIME_AVG.")"
speedup_std="(UNCERTAINTY_MULT(".\
                "(word(norms_avg,i+1)*$".OSTHRDS."/$".WTIME_AVG.")".\
              ", word(norms_avg,i+1)".\
              ", word(norms_std,i+1)".\
              ", $".WTIME_AVG.\
              ", $".WTIME_STD.\
                "))"

set ylabel "Speedup [normalized to 1-core walltime]"
set xlabel "Cores"

set xrange [0.5:@CORES+0.5]
set yrange [0:@CORES]

set ytics 0, 1, 16

set title TITLE."#1.1: Speedup vs Cores" 
set boxwidth @BARWIDTH
plot for [i=0:BLOCKS-1] INPUT index i \
    using @OSTHRDS_BAR:@speedup_avg:@speedup_std \
        with boxerrorbars ls (i+1) fill solid 0.5 title columnheader(1)

#set title TITLE."#1.b: Speedup vs Cores" 
#set boxwidth @BARWIDTH2
#plot for [i=BLOCKS-2:BLOCKS-1] INPUT index i \
#    using @OSTHRDS_BAR2:@speedup_avg:@speedup_std \
#        with boxerrorbars ls (i+1) fill solid 0.5 title columnheader(1)

set ytics autofreq

###############################################################################
# KTEPS

kteps_avg="(($".TASKS."/$".WTIME_AVG.")/1000)"
kteps_std="(UNCERTAINTY_MULT(".\
            "(($".TASKS."/$".WTIME_AVG.")/1000)".\
          ", $".WTIME_AVG.\
          ", $".WTIME_STD.\
          ", $".TASKS.\
          ", 0".\
            "))"

set ylabel "Threads Executed per Second [thousands]"
set xlabel "Cores"

set xrange [0.5:@CORES+0.5]
set yrange [*:*]

set title TITLE."#1.2: Threads Executed Per Second vs Cores" 
set boxwidth @BARWIDTH
plot for [i=0:BLOCKS-1] INPUT index i \
    using @OSTHRDS_BAR:@kteps_avg:@kteps_std \
        with boxerrorbars ls (i+1) fill solid 0.5 title columnheader(1)

###############################################################################
## OVERHEAD, IDLE-RATE 
###############################################################################

#set multiplot layout 2,1 scale 0.9

###############################################################################
# OVERHEAD

overhead1_avg="((($".WTIME_AVG."-($".TASKS."*($".DELAY."*1e-6)))/$".TASKS.")*1e6)"
overhead1_std="(UNCERTAINTY_MULT(".\
                "(($".WTIME_AVG."-($".TASKS."*($".DELAY."*1e-6)))/$".TASKS.")*1e6".\
              ", $".WTIME_AVG.\
              ", $".WTIME_STD.\
              ", $".TASKS.\
              ", 0".\
                "))"

set ylabel "CPU-Time Overhead per User HPX-Thread [Î¼sec]"
set xlabel "Cores"

set boxwidth @BARWIDTH

set xrange [0.5:@CORES+0.5]
set yrange [0:*]

set title TITLE."#2.1: CPU-Time Overhead per User HPX-Thread vs Cores (Derived from Walltime)" 
plot for [i=0:BLOCKS-1] INPUT index i \
    using @OSTHRDS_BAR:@overhead1_avg:@overhead1_std \
        with boxerrorbars ls (i+1) fill solid 0.5 title columnheader(1)

overhead2_avg="(((($".IRATE_AVG."/1000)*$".WTIME_AVG.")/$".TASKS.")*1e6)"
overhead2_std="(UNCERTAINTY_MULT(".\
                "((($".IRATE_AVG."/1000)*$".WTIME_AVG.")/$".TASKS.")*1e6".\
              ", $".IRATE_AVG.\
              ", $".IRATE_STD.\
              ", $".WTIME_AVG.\
              ", $".WTIME_STD.\
                "))"

set title TITLE."#2.2: CPU-Time Overhead per User HPX-Thread vs Cores (Derived from Idle-Rate and Walltime)\n" 
plot for [i=0:BLOCKS-1] INPUT index i \
    using @OSTHRDS_BAR:@overhead2_avg:@overhead2_std \
        with boxerrorbars ls (i+1) fill solid 0.5 title columnheader(1)

###############################################################################
# IDLE-RATE

set ylabel "Time Spent Scheduling [%]"
set xlabel "Cores"

set boxwidth @BARWIDTH

set xrange [0.5:@CORES+0.5]
set yrange [*:*]

irate1_avg="((($".WTIME_AVG."-(($".TASKS."*($".DELAY."*1e-6))))/$".WTIME_AVG.")*100)"
irate1_std="(UNCERTAINTY_MULT(".\
                "(($".WTIME_AVG."-(($".TASKS."*($".DELAY."*1e-6))))/$".WTIME_AVG.")*100".\
              ", $".WTIME_AVG.\
              ", $".WTIME_STD.\
              ", $".WTIME_AVG.\
              ", $".WTIME_STD.\
                "))"

set title TITLE."#2.3: % of Time Spent Scheduling vs Cores (Derived from Walltime)" 
plot for [i=0:BLOCKS-1] INPUT index i \
    using @OSTHRDS_BAR:@irate1_avg:@irate1_std \
        with boxerrorbars ls (i+1) fill solid 0.5 title columnheader(1)

irate2_avg="($".IRATE_AVG."/10)"
irate2_std="($".IRATE_STD."/10)"

set title TITLE."#2.4: % of Time Spent Scheduling vs Cores (Measured as Idle-Rate)" 
plot for [i=0:BLOCKS-1] INPUT index i \
    using @OSTHRDS_BAR:@irate2_avg:@irate2_std \
        with boxerrorbars ls (i+1) fill solid 0.5 title columnheader(1)

###############################################################################
## HPX-THREADS, PHASES
###############################################################################

#set multiplot layout 2,1 scale 0.9

###############################################################################
# HPX-THREADS 

hpxthrds_avg="($".HPXTHRDS_AVG."/1000)"
hpxthrds_std="($".HPXTHRDS_STD."/1000)"

set title TITLE."#3.1: HPX-Threads Executed vs Cores" 

set ylabel "HPX-Threads Executed [thousands]"
set xlabel "Cores"

set boxwidth @BARWIDTH

set xrange [0.5:@CORES+0.5]
set yrange [0:*]

plot for [i=0:BLOCKS-1] INPUT index i \
    using @OSTHRDS_BAR:@hpxthrds_avg:@hpxthrds_std \
        with boxerrorbars ls (i+1) fill solid 0.5 title columnheader(1)

###############################################################################
# PHASES 

phases_avg="($".PHASES_AVG."/1000)"
phases_std="($".PHASES_STD."/1000)"

set title TITLE."#3.2: HPX-Thread Phases Executed vs Cores" 

set ylabel "HPX-Thread Phases Executed [thousands]"
set xlabel "Cores"

set boxwidth @BARWIDTH

set xrange [0.5:@CORES+0.5]
set yrange [0:*]

plot for [i=0:BLOCKS-1] INPUT index i \
    using @OSTHRDS_BAR:@phases_avg:@phases_std \
        with boxerrorbars ls (i+1) fill solid 0.5 title columnheader(1)

###############################################################################
## CONTEXTS ALLOCATED, CONTEXT RECYCLES
###############################################################################

#set multiplot layout 2,1 scale 0.9

###############################################################################
# CONTEXTS ALLOCATED 

set title TITLE."#4.1: HPX-Thread Contexts Allocated vs Cores" 

set ylabel "HPX-Thread Contexts Allocated"
set xlabel "Cores"

set boxwidth @BARWIDTH

set xrange [0.5:@CORES+0.5]
set yrange [*:*]

plot for [i=0:BLOCKS-1] INPUT index i \
    using @OSTHRDS_BAR:@CTX_ALLOC_AVG:@CTX_ALLOC_STD \
        with boxerrorbars ls (i+1) fill solid 0.5 title columnheader(1)

###############################################################################
# CONTEXT RECYCLES

ctx_recy_avg="($".CTX_RECY_AVG."/1000)"
ctx_recy_std="($".CTX_RECY_STD."/1000)"

set title TITLE."#4.2: HPX-Thread Context Recycles vs Cores" 

set ylabel "HPX-Thread Context Recycles [thousands]"
set xlabel "Cores"

set boxwidth @BARWIDTH

set xrange [0.5:@CORES+0.5]
set yrange [*:*]

plot for [i=0:BLOCKS-1] INPUT index i \
    using @OSTHRDS_BAR:@ctx_recy_avg:@ctx_recy_std \
        with boxerrorbars ls (i+1) fill solid 0.5 title columnheader(1)

###############################################################################
## RESIDENT MEMORY, VIRTUAL MEMORY 
###############################################################################

#set multiplot layout 2,1 scale 0.9

###############################################################################
# RESIDENT MEMORY 

res_mem_avg="($".RES_MEM_AVG."/(1024*1024))"
res_mem_std="($".RES_MEM_STD."/(1024*1024))"

set title TITLE."#5.1: Resident Memory vs Cores" 

set ylabel "Resident Memory [MBytes]"
set xlabel "Cores"

set boxwidth @BARWIDTH

set xrange [0.5:@CORES+0.5]
set yrange [*:*]

plot for [i=0:BLOCKS-1] INPUT index i \
    using @OSTHRDS_BAR:@res_mem_avg:@res_mem_std \
        with boxerrorbars ls (i+1) fill solid 0.5 title columnheader(1)

###############################################################################
# VIRTUAL MEMORY 

vir_mem_avg="($".VIR_MEM_AVG."/(1024*1024))"
vir_mem_std="($".VIR_MEM_STD."/(1024*1024))"

set title TITLE."#5.2: Virtual Memory vs Cores" 

set ylabel "Virtual Memory [MBytes]"
set xlabel "Cores"

set boxwidth @BARWIDTH

set xrange [0.5:@CORES+0.5]
set yrange [*:*]

plot for [i=0:BLOCKS-1] INPUT index i \
    using @OSTHRDS_BAR:@vir_mem_avg:@vir_mem_std \
        with boxerrorbars ls (i+1) fill solid 0.5 title columnheader(1)

###############################################################################
## DTLB MISS RATE, L2 DATA MISS RATE
###############################################################################

#set multiplot layout 2,1 scale 0.9

###############################################################################
# DTLB MISS RATE

dtlb_rate_avg="(($".TLB_DM_AVG."/$".TOT_INS_AVG.")*100)"
dtlb_rate_std="(UNCERTAINTY_MULT(".\
                "(($".TLB_DM_AVG."/$".TOT_INS_AVG.")*100)".\
              ", $".TLB_DM_AVG.\
              ", $".TLB_DM_STD.\
              ", $".TOT_INS_AVG.\
              ", $".TOT_INS_STD.\
                "))"

set title TITLE."#6.1: DTLB Misses/Instructions Executed vs Cores" 

set ylabel "DTLB Misses/Instructions Executed [%]"
set xlabel "Cores"

set boxwidth @BARWIDTH

set xrange [0.5:@CORES+0.5]
set yrange [*:*]

plot for [i=0:BLOCKS-1] INPUT index i \
    using @OSTHRDS_BAR:@dtlb_rate_avg:@dtlb_rate_std \
        with boxerrorbars ls (i+1) fill solid 0.5 title columnheader(1)

###############################################################################
# L2 DATA MISS RATE

l2dm_rate_avg="(($".L2_DCM_AVG."/$".TOT_INS_AVG.")*100)"
l2dm_rate_std="(UNCERTAINTY_MULT(".\
                "(($".L2_DCM_AVG."/$".TOT_INS_AVG.")*100)".\
              ", $".L2_DCM_AVG.\
              ", $".L2_DCM_STD.\
              ", $".TOT_INS_AVG.\
              ", $".TOT_INS_STD.\
                "))"

set title TITLE."#6.2: L2 Data Misses/Instructions Executed vs Cores" 

set ylabel "L2 Data Misses/Instructions Executed [%]"
set xlabel "Cores"

set boxwidth @BARWIDTH

set xrange [0.5:@CORES+0.5]
set yrange [*:*]

plot for [i=0:BLOCKS-1] INPUT index i \
    using @OSTHRDS_BAR:@l2dm_rate_avg:@l2dm_rate_std \
        with boxerrorbars ls (i+1) fill solid 0.5 title columnheader(1)

