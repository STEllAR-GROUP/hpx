# Copyright (c) 2014 Thomas Heller
# Copyright (c) 2015 Andreas Schaefer
#
# SPDX-License-Identifier: BSL-1.0
# Distributed under the Boost Software License, Version 1.0. (See accompanying
# file LICENSE_1_0.txt or copy at http://www.boost.org/LICENSE_1_0.txt)

# Forward HPX_* cache variables
include("${CMAKE_CURRENT_LIST_DIR}/HPXCacheVariables.cmake")

# include exported targets if not already defined
if (NOT TARGET hpx_config)
  include("${CMAKE_CURRENT_LIST_DIR}/HPXModulesTargets.cmake")
endif()
if (NOT TARGET hpx)
  include("${CMAKE_CURRENT_LIST_DIR}/HPXTargets.cmake")
endif()

# include HPX cmake utilities
include("${CMAKE_CURRENT_LIST_DIR}/HPXMacros.cmake")

get_filename_component(_hpx_root_dir "${CMAKE_CURRENT_LIST_DIR}/../../.." ABSOLUTE)

set(HPX_VERSION_STRING "@HPX_VERSION@")
set(HPX_VERSION_MAJOR @HPX_VERSION_MAJOR@)
set(HPX_VERSION_MINOR @HPX_VERSION_MINOR@)
set(HPX_VERSION_SUBMINOR @HPX_VERSION_SUBMINOR@)

set(HPX_PREFIX "${_hpx_root_dir}")
set(HPX_DEBUG_POSTFIX "@HPX_DEBUG_POSTFIX@")
set(HPX_BUILD_TYPE "@CMAKE_BUILD_TYPE@")
set(HPX_CXX_STANDARD "@HPX_CXX_STANDARD@")

set(HPX_GIT_COMMIT "@HPX_WITH_GIT_COMMIT@" CACHE STRING "Revision of HPX from Git" FORCE)

set(HPX_C_COMPILER "@CMAKE_C_COMPILER@" CACHE STRING "C compiler for HPX" FORCE)
set(HPX_C_COMPILER_ID "@CMAKE_C_COMPILER_ID@" CACHE STRING "C compiler id for HPX" FORCE)
set(HPX_C_COMPILER_VERSION "@CMAKE_C_COMPILER_VERSION@" CACHE STRING "C compiler version for HPX" FORCE)

set(HPX_CXX_COMPILER "@CMAKE_CXX_COMPILER@" CACHE STRING "CXX compiler for HPX" FORCE)
set(HPX_CXX_COMPILER_ID "@CMAKE_CXX_COMPILER_ID@" CACHE STRING "CXX compiler id for HPX" FORCE)
set(HPX_CXX_COMPILER_VERSION "@CMAKE_CXX_COMPILER_VERSION@" CACHE STRING "CXX compiler version for HPX" FORCE)

set(HPX_Fortran_COMPILER "@CMAKE_Fortran_COMPILER@" CACHE STRING "Fortran compiler for HPX" FORCE)
set(HPX_Fortran_COMPILER_ID "@CMAKE_Fortran_COMPILER_ID@" CACHE STRING "Fortran compiler id for HPX" FORCE)
set(HPX_Fortran_COMPILER_VERSION "@CMAKE_Fortran_COMPILER_VERSION@" CACHE STRING "Fortran compiler version for HPX" FORCE)

##################################################
# Setup the imported libraries (publicly linked) #

# Allocator
set(HPX_JEMALLOC_ROOT "@JEMALLOC_ROOT@")
set(HPX_TCMALLOC_ROOT "@TCMALLOC_ROOT@")
set(HPX_TBBMALLOC_ROOT "@TBBMALLOC_ROOT@")
# Special handle for mimalloc cause we can't specify HPX_MIMALLOC_ROOT as a HINT
# to find_package
set(HPX_MIMALLOC_ROOT "@MIMALLOC_ROOT@")
if (NOT MIMALLOC_ROOT AND NOT "$ENV{MIMALLOC_ROOT}")
  set(MIMALLOC_ROOT ${HPX_MIMALLOC_ROOT})
endif()
include(HPX_SetupAllocator)

# Boost
# Separate boost targets to be unarily linked to some modules
set(HPX_BOOST_ROOT "@BOOST_ROOT@")
# By default BOOST_ROOT is set to HPX_BOOST_ROOT (not necessary for PAPI or
# HWLOC cause we are specifying HPX_<lib>_ROOT as an HINT to find_package)
if (NOT BOOST_ROOT AND NOT "$ENV{BOOST_ROOT}")
  set(BOOST_ROOT ${HPX_BOOST_ROOT})
endif()
# hpx::boost
include(HPX_SetupBoost)
# hpx::boost::filesystem
include(HPX_SetupBoostFilesystem)
# hpx::boost::program_options
include(HPX_SetupBoostProgramOptions)

# Hwloc
set(HPX_HWLOC_ROOT "@HWLOC_ROOT@")
include(HPX_SetupHwloc)

# Papi
set(HPX_PAPI_ROOT "@PAPI_ROOT@")
include(HPX_SetupPapi)

# CUDA
include(HPX_SetupCUDA)
##################################################

set(HPX_WITH_MALLOC_DEFAULT @HPX_WITH_MALLOC@)

if(HPX_WITH_DATAPAR_VC AND NOT Vc_DIR)
  set(Vc_DIR "@Vc_DIR@")
endif()
include(HPX_SetupVc)

if(NOT HPX_CMAKE_LOGLEVEL)
  set(HPX_CMAKE_LOGLEVEL "WARN")
endif()

enable_language(C)

hpx_check_cmake_build_type()
hpx_check_compiler_compatibility()
hpx_check_boost_compatibility()
hpx_setup_allocator()

if(NOT DEFINED ${CMAKE_FIND_PACKAGE_NAME}_FOUND)
  set(${CMAKE_FIND_PACKAGE_NAME}_FOUND true)
endif()

# Set legacy variables for linking and include directories
set(HPX_LIBRARIES "hpx;hpx_init;")
set(HPX_INCLUDE_DIRS "")
set(HPX_LIBRARY_DIR "")
if(HPX_WITH_DYNAMIC_HPX_MAIN AND (("${CMAKE_SYSTEM_NAME}" STREQUAL "Linux") OR (APPLE)))
  set(HPX_LINK_LIBRARIES "hpx_wrap;")
else()
  set(HPX_LINK_LIBRARIES "")
endif()
# The flags required by hpx_wrap are propagated from its interface options.
set(HPX_LINKER_FLAGS "")
